/#
Usage: kiwi configure [options]
Options:
  h, help                      print this message
  i, install                   install interpreter & standard library
  u, uninstall                 uninstall interpreter & standard library
#/

global.config = {
  "installation": { "bin": null, "lib": null },
  "source": { "bin": null, "lib": null },
}

fn main(args)
  global.config.installation.bin    = "/usr/bin/kiwi"       # Default path to install the Kiwi interpreter.
  global.config.installation.lib    = "/usr/lib/kiwi"       # Default path to install the Kiwi standard library.
  global.config.source.bin          = env::kiwi()           # Path to the local Kiwi executable.
  global.config.source.lib          = env::kiwilib()        # Path to the local Kiwi standard library.

  if args.size() != 1
    return print_help()
  end

  opt = args[0].lowercase().replace('-|\/', "") # remove dashes and slashes

  if ["i", "install"].contains(opt)
    install_kiwi()
    println("Kiwi installed successfully.")
  elsif ["u", "uninstall"].contains(opt)
    uninstall_kiwi()
    println("Kiwi has been uninstalled.")
  else
    print_help()
  end
end

fn print_help()
  flags = [
    ["h, help", "print this message"],
    ["i, install", "install interpreter & standard library"],
    ["u, uninstall", "uninstall interpreter & standard library"]
  ]
  
  println("Usage: kiwi configure [options]\nOptions:")

  flags.each(with (flag) do
    println("  " + string::padend(flag[0], 30, " ") + flag[1])
  end)
end

fn sudo_check()
  euid = sys::euid()  # sys::euid() will return -1 on non-POSIX systems.

  if euid != 0
    println("Please run this script with `sudo`.")
    exit(1)
  end
end

fn uninstall_kiwi()
  sudo_check()

  ec = 0 # an error counter

  if fs::exists(global.config.installation.bin)
    ec += fs::remove(global.config.installation.bin) ? 0 : 1
  end
  
  if fs::isdir(global.config.installation.lib)
    ec += fs::rmdirf(global.config.installation.lib) > 0 ? 0 : 1
  end
  
  # Check for errors in removal process
  if ec != 0
    println("Failed to remove existing Kiwi installation.")
    exit(1)
  end
end

fn install_kiwi()
  if global.config.source.bin == global.config.installation.bin
    println("Kiwi is already installed.")
    exit(0)
  end

  uninstall_kiwi()
  
  ec = 0 # an error counter

  if fs::exists(global.config.source.bin)
    ec += fs::copy(global.config.source.bin, global.config.installation.bin) ? 0 : 1
  end
  
  if fs::isdir(global.config.source.lib)
    ec += fs::copyr(global.config.source.lib, global.config.installation.lib) ? 0 : 1
  end

  # Check for errors in installation process
  if ec != 0
    println("Failed to install kiwi. Please check permissions and try again.")
    exit(1)
  end
end

main(argv::get())