fn main()
  println "Begin Kiwi test suite...\n"

  tests_start = time::ticks()
  results = run_tests()
  duration = time::ticksms(time::ticks() - tests_start)

  println "\nRan ${results.size()} test(s) in ${duration}ms\n"
  succeeded = 0
  failed = 0

  for r in results do
    println "${r.name}: ${(r.result ? "passed" : "failed")}"
    if r.result
      succeeded += 1
    else
      failed += 1
    end
  end

  println ""

  if failed == 0
    println "All tests passed!"
  else
    println "${succeeded} tests passed, ${failed} tests failed."
  end
end

global.tests = {}
global.test_results = []

fn assert(condition, msg = "Assertion failed.")
  throw msg when condition == false
end

fn register_test(name, t)
  global.tests.set(name, t)
end

fn run_test(name, test, results = [])
  passed = false
  try
    println "running ${name}"
    test()
    passed = true
  catch (err)
    println err
  end
  results.push({ "name": t, "result": passed })
end

fn run_tests()
  results = []
  for t in global.tests do
    run_test(t, global.tests.get(t), results)
  end
  return results
end

register_test("addition", with () do
  x = 1
  x += 2
  assert(x == 3)
end)

register_test("subtraction", with () do
  x = 1
  x -= 2
  assert(x == -1)
end)

register_test("multiplication", with () do
  x = 1
  x *= 2
  assert(x == 2)
end)

register_test("division", with () do
  x = 1
  x /= 2.
  assert(x == 0.5)
end)

register_test("truthiness", with () do
  assert(!null.truthy())      # null is never truthy
  assert(!(0).truthy())       # 0 is the only non-truthy number
  assert((1).truthy())        # non-zero numbers are always truthy
  assert(!"".truthy())        # empty strings are not truthy
  assert("0".truthy())        # non-empty strings are truthy
  assert(![].truthy())        # empty lists are not truthy
  assert([0].truthy())        # non-empty lists are truthy
  assert(!{}.truthy())        # empty hashes are not truthy
  assert({'a': 1}.truthy())   # non-empty hashes are truthy
  assert(!false.truthy())     # false is never truthy
  assert(true.truthy())       # true is always truthy
end)

main()