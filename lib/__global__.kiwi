/#
@summary The __global__ file contains global definitions.
#/

const KIWI_STDLIB_VERSION = "1.3.7"

const STDLIB_PACKAGES = [
  "hashable", "compress", "console", "crypto", "csv",
  "env", "fio", "http", "iter", "list", "path", "math", "socket", "tls",
  "std", "std::error", "stdin", "string", "sys", "task", "tester", "time",
  "reflector"
]

for pkg in STDLIB_PACKAGES do
  export pkg
end

/#
@summary Creates a date value.
@param any year : A year as an integer, or a string representing a date.
@param integer month : The month. Defaults to 1 (January).
@param integer day : The day. Defaults to 1 (first of the month).
@param integer hour : The hour. Defaults to 0 (midnight).
@param integer minute : The minute. Defaults to 0.
@param integer second : The second. Defaults to 0.
@param integer millisecond : The millisecond. Defaults to 0.
@return date : Returns a date value.
#/
fn d
(
  year:         any     = time::now().to_string(),
  month:        integer = 1,
  day:          integer = 1,
  hour:         integer = 0,
  minute:       integer = 0,
  second:       integer = 0,
  millisecond:  integer = 0
): date
  return year.to_date() when year.is_a(string)

  var
  (
    y:          string = "${year}",
    m:          string = ("${month}").padstart(2, "0"),
    d:          string = ("${day}").padstart(2, "0"),
    h:          string = ("${hour}").padstart(2, "0"),
    mi:         string = ("${minute}").padstart(2, "0"),
    s:          string = ("${second}").padstart(2, "0"),
    ms:         string = ("${millisecond}").padstart(3, "0")
  )

  ("${y}/${m}/${d} ${h}:${mi}:${s}.${ms}").to_date()
end

/#
@summary Returns a unicode character without requiring escaping.
@param any code : A code (either an integer or a hex string).
@example
---
println char(0x1F95D)          # ğŸ¥
println char("U+1F40D")        # ğŸ
println char(0x2764)           # â¤ï¸
println char(0x25A0)           # â–  
println char("1F600")          # ğŸ˜Š
---
#/
fn char(code)
  # code can be integer or hex string
  if code.is_a(integer)
    hex = code.to_hex().padstart(8, "0").uppercase()
  else
    hex = code.uppercase().trim().replace("0x", "").replace("U+", "")
    if hex.size() < 4
      hex = hex.padstart(4, "0")
    elsif hex.size() > 8
      hex = hex[hex.size()-8:]  # truncate to last 8 digits
    end
  end
    
  hex = hex.size() < 8 ? hex.padstart(8, "0") : hex

  code_str = "\"\\U${hex}\""  
  lambda_str = "(with () do ${code_str} end)()"

  # I know.
  eval(lambda_str)
end

/#
@summary Awaits a task.
@param integer task_id : The task identifier.
@return any : Returns the result of the task.
#/
fn await(task_id: integer): any
  task::await(task_id)
end

/#
@summary Spawns a task.
@param lambda async_task : The task to execute as a lambda.
@param list args : The arguments for the task.
@return integer : Returns a task identifier.
#/
fn spawn(async_task: lambda, args: list = []): integer
  task::spawn(async_task, args)
end

/#
@summary Prompts for user input.
@param string msg : The prompt.
@return string : Returns the user input.
#/
fn input(msg: string = ""): string
  __console_readline__()
end

/#
@summary Used to check if the script is the entrypoint.
@return boolean : Returns `true` when the code is executed as a script.
#/
fn __main__(): boolean
  __entrypath__() == __execpath__()
end