package collections
end

/# An implementation for working with heaps. #/

struct Heap
  fn new(type = "min")
    @heap = []
    @heap_type = ["min", "max"].contains(type) ? type : "min"
  end

  fn insert(value)
    @heap.push(value)
    heapify_up(@heap.size() - 1)
  end

  fn extract_root()
    throw "Heap is empty" when @heap.empty()

    root = @heap.get(0)
    
    if @heap.size() > 1
      @heap.set(0, @heap.pop())  # Move the last element to the root
      heapify_down(0)
    else
      @heap.pop()  # Remove the last element if it's the only one left
    end

    return root
  end

  fn peek()
    throw "Heap is empty" when @heap.empty()
    return @heap.get(0)
  end

  fn size()
    return @heap.size()
  end

  fn is_empty()
    return @heap.empty()
  end

  private fn heapify_up(index)
    parent_index = (index - 1) / 2

    if @heap_type == "min"
      while index > 0 && @heap.get(index) < @heap.get(parent_index) do
        swap(index, parent_index)
        index = parent_index
        parent_index = (index - 1) / 2
      end
    else
      while index > 0 && @heap.get(index) > @heap.get(parent_index) do
        swap(index, parent_index)
        index = parent_index
        parent_index = (index - 1) / 2
      end
    end
  end

  private fn heapify_down(index)
    left_child_index = 2 * index + 1
    right_child_index = 2 * index + 2
    size = @heap.size()

    if @heap_type == "min"
      smallest = index

      if left_child_index < size && @heap.get(left_child_index) < @heap.get(smallest)
        smallest = left_child_index
      end
      
      if right_child_index < size && @heap.get(right_child_index) < @heap.get(smallest)
        smallest = right_child_index
      end
      
      if smallest != index
        swap(index, smallest)
        heapify_down(smallest)
      end
    else
      largest = index
      
      if left_child_index < size && @heap.get(left_child_index) > @heap.get(largest)
        largest = left_child_index
      end
      
      if right_child_index < size && @heap.get(right_child_index) > @heap.get(largest)
        largest = right_child_index
      end
      
      if largest != index
        swap(index, largest)
        heapify_down(largest)
      end
    end
  end

  private fn swap(i, j)
    @heap.swap(i, j)
  end
end

/# An implementation for working with sets. #/

struct Set
  fn new(data = [])
    @data = data.unique()
  end

  fn add(item)
    new_data = (@data).clone()
    new_data.push(item)
    @data = new_data.flatten().unique()
  end

  fn remove(item)
    new_data = to_list().select(with (n) do return n != item end)
    @data = new_data
  end

  fn contains(item)
    return to_list().contains(item)
  end

  fn size()
    return to_list().size()
  end

  fn clear()
    @data = []
  end

  fn difference(data)
    input_data = get_input_data(data)
    new_data = to_list()
    return Set.new(new_data.select(with (n) do return !input_data.contains(n) end))
  end

  fn disjoint(data)
    return !intersect(data)
  end

  fn intersect(data)
    input_data = get_input_data(data)
    new_data = to_list()
    return !new_data.select(with (n) do return input_data.contains(n) end).empty()
  end

  fn merge(data)
    input_data = get_input_data(data)
    new_data = to_list()
    new_data.push(input_data)
    @data = new_data.flatten().unique()
  end

  fn union(data)
    input_data = get_input_data(data)
    new_data = to_list()
    new_data.push(input_data)
    return Set.new(new_data.flatten())
  end

  fn subset(data)
    input_data = get_input_data(data)
    new_data = to_list()
    return input_data.select(with (n) do return new_data.contains(n) end).size() == new_data.size()
  end

  fn superset(data)
    input_data = get_input_data(data)
    new_data = to_list()
    return input_data.select(with (n) do return new_data.contains(n) end).size() == input_data.size()
  end

  fn to_list()
    return ((@data).clone())
  end

  fn to_string()
    return to_list().to_string()
  end

  fn type()
    return "Set"
  end

  private fn get_input_data(data)
    input_data = []
    if data.type() == "Set"
      input_data = data.to_list()
    elsif data.is_a(List)
      input_data = data
    else
      throw "Expected a List or a Set."
    end
    return input_data
  end
end

export "collections"