/#
@summary A package for working with environment variables.
#/
package env
  /#
  Parse command-line arguments into key-value pairs.
  #/
  fn parse_args(args: list = [], value_opts: list = []): hashmap
    args = args.empty() ? env::argv().keys() : args

    cfg = {}
    it = list::iterator(args)

    while it.can_read() do
      opt = it.consume()

      if value_opts.contains(opt)
        if not it.can_read()
          throw "Expected a parameter value for option `${opt}`"
        end

        cfg[opt] = it.consume()
      else
        cfg[opt] = true
      end
    end

    cfg
  end

  /#
  @summary Get the hashmap of command-line arguments.
  @return Hashmap of command-line arguments.
  #/
  fn argv(): hashmap
    return __env_argv__()
  end

  /#
  @summary Get a named option value by key.
  @param _key The option key.
  @return String containing the option value.
  #/
  fn opt(_key: string): string
    return __env_xarg__(_key)
  end

  /#
  @summary Get all environment variables as a hashmap.
  @return Hashmap representing all environment variables.
  #/
  fn all(): hashmap
    return __env_getall__()
  end

  /#
  @summary Get an environment variable.
  @param _var_name The environment variable name.
  @return The environment variable value, an empty string if not found.
  #/
  fn get(_var_name: string): string
    return __env_getenv__(_var_name)
  end

  /#
  @summary Set an environment variable.
  @param _var_name The environment variable name.
  @param _var_value The environment variable value.
  @return A flag indicating success or failure.
  #/
  fn set(_var_name: string, _var_value: string): boolean
    return __env_setenv__(_var_name, _var_value)
  end

  /#
  @summary Get the path to Kiwi.
  @return The path to Kiwi.
  #/
  fn bin(): string
    return __env_bin__()
  end

  /#
  @summary Get the path to the Kiwi Standard Library.
  @return The path to the Kiwi Standard Library.
  #/
  fn lib(): string
    return __env_lib__()
  end
end

export "env"