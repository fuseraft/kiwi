#!/usr/bin/env kiwi

fn main()
  server_sock = socket::tcpserver("127.0.0.1", 8080)
  main_task = spawn(accept_loop, [server_sock, req_handler])
end

/#
listens for new requests
#/
accept_loop = with (server: integer, handler: lambda) do
  while true do
    pending_accept = socket::accept(server)
    client_sock = await(pending_accept)  
    handler_task = spawn(handler, [client_sock])
  end
end

/#
handles requests
#/
req_handler = with (client: integer) do
  buffer = [].to_bytes()

  recv_task = socket::recv(client, 8192)
  data = await(recv_task)

  if data.size() > 0
    buffer = buffer.concat(data)
    req = http::parse(buffer)
    
    if req != null
      resp = http::html_response(200, 
        ('<!DOCTYPE html>
          <html lang="en">
          <head>
            <meta charset="UTF-8">
            <meta name="viewport" content="width=device-width, initial-scale=1.0">
            <meta http-equiv="X-UA-Compatible" content="ie=edge">
            <title>from kiwi</title>
          </head>
          <body>' +
            "
            <h1>Sockets are working!</h1>
            <p>You sent: ${req.method} ${req.path}</p>
            " +
          '</body>
          </html>').dedent())
      send = socket::send(client, resp)
      send_res = await(send)
    end
  end
  
  socket::close(client)
end

main()