/#
Usage: kiwi server
#/

import "@kiwi/fs"
import "@kiwi/string"
import "@kiwi/web"

def generate_directory_listing(dir_path) 
  html = "<html><body><h1>Index of ${dir_path}</h1><ul>"

  paths = fs::listdir(dir_path)
  dir_path = string::urldecode(dir_path)

  for entry in paths do
    display_name = fs::filename(entry)
    path_name = string::urlencode(fs::combine(dir_path.substring(2), display_name))
    
    if display_name.contains("ü•ù")
      display_name = display_name.replace("ü•ù", "kiwi")
    end

    if fs::isdir(entry)
      html += "<li><a href=\"/${path_name}/\">${display_name}/</a></li>"
    else
      html += "<li><a href=\"/${path_name}\">${display_name}</a></li>"
    end
  end
  
  html += "</ul></body></html>"
  return html
end

web::get('((/.*)?)', with (req) do
  req_path = string::urldecode(".${req.path}").replace("\.\/\.\/", "\.\/")
  res = {}

  # Serve files and directories
  if fs::isdir(req_path)
    dir_list = generate_directory_listing(req_path)
    res = web::ok(dir_list, "text/html")
  elsif fs::exists(req_path)
    content = fs::read(req_path)
    if !content.empty()
      res = web::ok(content, "text/plain")
    else
      res = web::bad("File not found", "text/plain", 404)
    end
  else
    res = web::bad("File not found", "text/plain", 404)
  end

  return res
end)

println("Server starting at http://localhost:8080/")
web::listen("0.0.0.0", 8080)