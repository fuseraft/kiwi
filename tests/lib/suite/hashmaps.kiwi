/# ==============================================================================
    Testing core hashmap functionality and common builtins.
   ============================================================================== #/
tester::register_test("hashmaps", with do
  var (hash: hashmap = {
    key1: true, 
    key2: 1, 
    key3: ["a", "b", "c"]
  })

  var (other_hashmap: hashmap)

  for key in hash.keys() do 
    other_hashmap[key] = hash[key]
  end
  
  other_hashmap = {}
  other_hashmap = hash.merge(other_hashmap)
  
  for k in hash.keys() do
    tester::assert(other_hashmap.has_key(k), "A1")
    tester::assert(other_hashmap.get(k).to_string() == hash.get(k).to_string(), "A2")
  end

  other_hashmap.remove("key1")
  tester::assert(other_hashmap.keys().sort().join() == "key2key3", "A3")
  tester::assert(other_hashmap.values().sort().first() == 1, "A4")

  other_hashmap.clear()
  tester::assert(other_hashmap.empty(), "A5")

  other_hashmap.hello = "world"
  tester::assert(other_hashmap.get("hello") == "world", "A6")
  
  other_hashmap.set("hello", "kiwi")
  tester::assert(other_hashmap.hello == "kiwi", "A7")
end)