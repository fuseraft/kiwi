#!/usr/bin/env kiwi

fn main()
  server_sock = __socket_tcpserver__("127.0.0.1", 8080)
  main_task = task::spawn(accept_loop, [server_sock, req_handler])
end

/#
listens for new requests
#/
accept_loop = with (server: integer, handler: lambda) do
  while true do
    pending_accept = __socket_accept__(server)
    client_sock = task::await(pending_accept)  
    handler_task = task::spawn(handler, [client_sock])
  end
end

/#
handles requests
#/
req_handler = with (client: integer) do
  buffer = [].to_bytes()

  recv_task = __socket_recv__(client, 8192)
  data = task::await(recv_task)

  if data.size() > 0
    buffer = buffer.concat(data)
    req = http::parse(buffer)
    
    if req != null
      resp = http::html(200, 
        string::dedent('<!DOCTYPE html>
                        <html lang="en">
                        <head>
                          <meta charset="UTF-8">
                          <meta name="viewport" content="width=device-width, initial-scale=1.0">
                          <meta http-equiv="X-UA-Compatible" content="ie=edge">
                          <title>from kiwi</title>
                        </head>
                        <body>' +
                          "
                          <h1>Sockets are working!</h1>
                          <p>You sent: ${req.method} ${req.path}</p>
                          " +
                        '</body>
                        </html>'))
      send = __socket_send__(client, resp)
      send_res = task::await(send)
    end
  end
  
  __socket_close__(client)
end

main()