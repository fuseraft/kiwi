/#
 The Kiwi Programming Language 2.0.3
 #/

fn testsuite()
  println "Running Kiwi Test Suite ...\n"

  results = guava::run_tests()

  succeeded = 0
  failed = 0
  duration = 0

  for r in results do
    duration += r.duration

    if r.result
      succeeded += 1
    else
      failed += 1
    end
  end

  println "\nRan ${results.size()} test(s) in ${duration}ms\n"

  if failed == 0
    println "All tests passed!"
  else
    println "${succeeded} tests passed, ${failed} tests failed."
  end
end

guava::register_test("truthiness", with () do
  guava::assert(!null.truthy())      # null is never truthy
  guava::assert(!(0).truthy())       # 0 is the only non-truthy number
  guava::assert((1).truthy())        # non-zero numbers are always truthy
  guava::assert(!"".truthy())        # empty strings are not truthy
  guava::assert("0".truthy())        # non-empty strings are truthy
  guava::assert(![].truthy())        # empty lists are not truthy
  guava::assert([0].truthy())        # non-empty lists are truthy
  guava::assert(!{}.truthy())        # empty hashes are not truthy
  guava::assert({'a': 1}.truthy())   # non-empty hashes are truthy
  guava::assert(!false.truthy())     # false is never truthy
  guava::assert(true.truthy())       # true is always truthy
end)

guava::register_test("operators", with () do
  # arithmetic
  guava::assert(10 + 20 == 30)   # addition
  guava::assert(10 - 20 == -10)  # subtraction
  guava::assert(10 * 20 == 200)  # multiplication
  guava::assert(10 / 20. == 0.5) # division
  guava::assert(10 % 2 == 0)     # modulo division
  guava::assert(10 ** 2 == 100)  # exponentiation
  
  # logical or 
  guava::assert(!(false || false))
  guava::assert(false || true)
  guava::assert(true || false)
  guava::assert(true || true)

  # logical and
  guava::assert(!(false && false))
  guava::assert(!(false && true))
  guava::assert(!(true && false))
  guava::assert(true && true)

  # logical not
  guava::assert(false == !true)
  guava::assert(!!true)
  guava::assert(!false)

  # comparison
  guava::assert(1 < 2)
  guava::assert(2 > 1)
  guava::assert(1 <= 1)
  guava::assert(1 >= 1)
  guava::assert(1 == 1)
  guava::assert(1 != 0)

  # bitwise operators
  a = 5
  b = 9
  guava::assert(a & b == 1)
  guava::assert(a | b == 13)
  guava::assert(a ^ b == 12)
  guava::assert(a << 1 == 10)
  guava::assert(a >> 1 == 2)
  guava::assert(~a == -6)

  # bitswap
  old_a = a
  old_b = b
  a = a ^ b
  b = a ^ b
  a = a ^ b
  guava::assert(b == old_a)
  guava::assert(a == old_b)
end)

guava::register_test("strings", with () do
  # simple concatenation
  a = "Hello"
  b = " World"
  guava::assert(a + b == "Hello World")

  # concatenation expression
  a = "Hello"
  b = "World"
  guava::assert((a + " " + b) == "Hello World")

  # string interpolation
  guava::assert("${a} ${b}" == "Hello World")

  wrap_string = with (inputstring, openchar, closechar) do
    return "${openchar}${inputstring}${closechar}"
  end

  guava::assert("${wrap_string("div", "<", ">")}" == "<div>")

  # string multiplication
  a = "Hello"
  b = 5
  guava::assert(a * b == "HelloHelloHelloHelloHello")
end)

guava::register_test("lists", with () do
  # Basic List Operations
  basic_list = [1, 2, 3]
  guava::assert(basic_list.size() == 3)
  guava::assert(basic_list[0] == 1)
  guava::assert(basic_list[1] == 2)
  guava::assert(basic_list[2] == 3)
  # List Insertion
  basic_list.push(4)
  guava::assert(basic_list == [1, 2, 3, 4])
  # List Concatenation
  other_list = [5, 6, 7]
  combined_list = basic_list.concat(other_list)
  guava::assert(combined_list == [1, 2, 3, 4, 5, 6, 7])

  # List Joining
  words = ["The", "Kiwi", "Programming", "Language"]
  guava::assert(words.join(" ") == "The Kiwi Programming Language")
  guava::assert(words.join("-") == "The-Kiwi-Programming-Language")
  guava::assert(words.join("") == "TheKiwiProgrammingLanguage")
  guava::assert(words.join() == "TheKiwiProgrammingLanguage")

  # List Slicing
  sliced_list = combined_list[2:5]
  guava::assert(sliced_list == [3, 4, 5])

  # List Reversal
  reversed_list = combined_list[::-1]
  guava::assert(reversed_list == [7, 6, 5, 4, 3, 2, 1])

  # Nested Lists and Flattening
  nested_list = [1, 2, [3, 4], [5, [6, 7]]]
  guava::assert(nested_list.flatten() == [1, 2, 3, 4, 5, 6, 7])

  # String to List Conversion and Manipulation
  char_list = "abcdef".chars()
  guava::assert(char_list == ["a", "b", "c", "d", "e", "f"])

  # Empty List Verification
  new_list = []
  while !char_list.empty() do
    new_list.push(char_list.first())
    char_list.remove_at(0)
  end
  guava::assert(char_list.empty())
  guava::assert(new_list == ["a", "b", "c", "d", "e", "f"])

  # List Initialization with a Range
  range_list = [1..5]
  guava::assert(range_list == [1, 2, 3, 4, 5])

  a = [1..10]
  b = a.select(with (i) do i % 2 == 0 end)
  c = a - b
  d = []
  d += b
  d += c

  guava::assert(d == [2, 4, 6, 8, 10, 1, 3, 5, 7, 9])
end)


guava::register_test("sets", with () do
  s1 = Set.new([1, 2, 3])
  s2 = Set.new([1, 2])
  s3 = Set.new([4, 5])
  s4 = Set.new([1, 2, 3, 4])

  guava::assert(s1.subset(s2) == false)
  guava::assert(s2.subset(s1) == true)
  guava::assert(s3.subset(s1) == false)
  guava::assert(s2.subset(s2) == true)
  guava::assert(s2.subset(s4) == true)
  guava::assert(s1.superset(s2) == true)
  guava::assert(s2.superset(s1) == false)
  guava::assert(s1.superset(s3) == false)
  guava::assert(s2.superset(s2) == true)
  guava::assert(s4.superset(s2) == true)
  
  a1 = Set.new([3,6,4])
  a2 = Set.new([1,2,3,4,5,6,7,8,9])
  
  a3 = a2.difference(a1)
  guava::assert(a3.to_list() == [1,2,5,7,8,9])
  
  a3.clear()
  guava::assert(a3.to_list().empty())
end)

guava::register_test("heaps", with () do
  min_heap = Heap.new("min")
  max_heap = Heap.new("max")

  # Insert elements into the min-heap
  min_heap.insert(5)
  min_heap.insert(3)
  min_heap.insert(8)
  min_heap.insert(1)

  # Extract root from min-heap
  guava::assert(min_heap.extract_root() == 1)
  guava::assert(min_heap.extract_root() == 3)

  # Insert elements into the max-heap
  max_heap.insert(5)
  max_heap.insert(3)
  max_heap.insert(8)
  max_heap.insert(1)

  # Extract root from max-heap
  guava::assert(max_heap.extract_root() == 8)
  guava::assert(max_heap.extract_root() == 5)
end)

guava::register_test("hashes", with () do
  hash = {
    "key1": true, 
    "key2": 1, 
    "key3": ["a", "b", "c"]
  }

  other_hash = {}

  for key in hash.keys() do 
    other_hash[key] = hash[key] 
  end
  
  guava::assert(other_hash == hash)

  other_hash = {}
  other_hash = hash.merge(other_hash)
  guava::assert(other_hash == {"key1": true, "key2": 1, "key3": ["a", "b", "c"]})

  other_hash.remove("key1")
  guava::assert(other_hash.keys() == ["key2", "key3"])
  guava::assert(other_hash.values() == [1, ["a", "b", "c"]])

  other_hash.clear()
  guava::assert(other_hash.empty())

  other_hash.hello = "world"
  guava::assert(other_hash.get("hello") == "world")
  
  other_hash.set("hello", "kiwi")
  guava::assert(other_hash.hello == "kiwi")
end)

guava::register_test("dates", with () do
  d = DateTime.new(DateTime.now().get_year(), 1, 2).add_days(-1).add_hours(-1).add_minutes(-1).add_seconds(-1)
  d2 = DateTime.now()

  guava::assert(d.before(d2))
  guava::assert(d2.after(d))
end)

guava::register_test("timespans", with () do
  # Test case 1: Same date
  d1 = DateTime.new(2024, 10, 1)
  d2 = DateTime.new(2024, 10, 1)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 0 years, 0 months, 0 days, 0 hours, 0 minutes, 0 seconds")

  # Test case 2: One day difference
  d1 = DateTime.new(2024, 10, 2)
  d2 = DateTime.new(2024, 10, 1)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 0 years, 0 months, 1 days, 0 hours, 0 minutes, 0 seconds")

  # Test case 3: One month difference
  d1 = DateTime.new(2024, 11, 1)
  d2 = DateTime.new(2024, 10, 1)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 0 years, 1 months, 0 days, 0 hours, 0 minutes, 0 seconds")

  # Test case 4: One year difference
  d1 = DateTime.new(2025, 10, 1)
  d2 = DateTime.new(2024, 10, 1)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 1 years, 0 months, 0 days, 0 hours, 0 minutes, 0 seconds")

  # Test case 5: Leap year handling (Feb 29)
  d1 = DateTime.new(2024, 3, 1)
  d2 = DateTime.new(2024, 2, 29)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 0 years, 0 months, 1 days, 0 hours, 0 minutes, 0 seconds")

  # Test case 6: Multiple years and months difference
  d1 = DateTime.new(2026, 12, 15)
  d2 = DateTime.new(2023, 7, 5)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 3 years, 5 months, 10 days, 0 hours, 0 minutes, 0 seconds")

  # Test case 7: Time difference within the same day
  d1 = DateTime.new(2024, 10, 1, 12, 30, 15)
  d2 = DateTime.new(2024, 10, 1, 10, 15, 10)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 0 years, 0 months, 0 days, 2 hours, 15 minutes, 5 seconds")

  # Test case 8: Time difference over midnight
  d1 = DateTime.new(2024, 10, 2, 1, 30, 45)
  d2 = DateTime.new(2024, 10, 1, 23, 45, 30)
  res = d1.diff(d2)
  guava::assert(res.to_string() == "TimeSpan: 0 years, 0 months, 0 days, 1 hours, 45 minutes, 15 seconds")
end)

guava::register_test("serialization", with () do
  a = {}
  a.x = "hello"
  a.y = {}
  a.y.msg = "hello, world!"

  str = serialize(a)
  b = deserialize(str)

  guava::assert(str == a.to_string())
  guava::assert(b.to_string() == a.to_string())
end)

guava::register_test("classes", with () do
  class Shape
    def area() end
    def perimeter() end
  end

  class Circle < Shape
    def initialize(radius)
      @radius = radius
    end

    def area()
      return 3.14159 * @radius * @radius
    end

    def perimeter()
      return 2 * 3.14159 * @radius
    end
  end

  circle = Circle.new(5)

  # magic numbers everywhere
  guava::assert(math::floor(circle.area()).to_int() == 78)
  guava::assert(math::floor(circle.perimeter()).to_int() == 31)
end)

guava::register_test("builtins", with () do
  a = "FooBar".lowercase() 
  guava::assert("${a}${a.uppercase()}" == "foobarFOOBAR")
  guava::assert(a.index("bar") == 3)
  guava::assert(a.chars().size() == 6)
  guava::assert("foobar".replace("foo", "food") == "foodbar")
  guava::assert(!a.begins_with("food"))
  guava::assert("foobar".ends_with("bar"))
  guava::assert(!"foobar".ends_with("bark"))
end)

guava::register_test("standard library", with () do
  guava::assert(string::mirror("hello") == "helloolleh")
end)

guava::register_test("nulls", with () do
  e = {"a": null, "b": null}
  guava::assert(e == {"a": null, "b": null})

  e.a = false
  e.b = 'hello'
  guava::assert(e == {"a": false, "b": "hello"})

  e.a = null
  e.b = null
  guava::assert(e == {"a": null, "b": null})

  e = null
  guava::assert(e == null)

  e = 0
  guava::assert(e != null)

  guava::assert(!null)
  guava::assert(!!!null)
end)

guava::register_test("type checks", with () do
  s = "kiwi"
  i = 100
  d = 3.14159
  b = true
  h = {'a': [123, 456, "789"]}

  guava::assert(s.is_a(String))
  guava::assert(i.is_a(Integer))
  guava::assert(d.is_a(Double))
  guava::assert(b.is_a(Boolean))
  guava::assert(null.is_a(None))
  guava::assert(h.is_a(Hash))
  guava::assert(h.a.is_a(List))
end)

guava::register_test("loops", with () do
  fruits = ["kiwi", "strawberry", "pineapple", "lime", "mango", "guava"]
  list = []

  # iterate list
  for fruit in fruits do
    list.push(fruit)
  end

  guava::assert(list == ["kiwi", "strawberry", "pineapple", "lime", "mango", "guava"])
  list.clear()

  # iterate index
  for fruit, index in fruits do 
    list.push(index)
  end

  guava::assert(list == [0, 1, 2, 3, 4, 5])
  list.clear()

  # iterate inline list
  for item in ["kiwi", "is", "fun"] do
    list.push(item)
  end

  guava::assert(list == ["kiwi", "is", "fun"])
  list.clear()

  # iterate an inline list with an index
  for item, index in ["kiwi", "is", "fun"] do
    list.push(index)
  end

  guava::assert(list == [0, 1, 2])
  list.clear()

  # iterate a range
  for i in [0..5] do
    list.push(i)
  end

  guava::assert(list == [0, 1, 2, 3, 4, 5])
  list.clear()

  # nested while loops
  i = 0
  test_sum = 0
  while i < 10 do
    i += 1
    j = 0
    
    while j < 10 do
      j += 1
      test_sum += 1
    end
  end

  guava::assert(test_sum == 100)

  list = []
  i = 1
  while i <= 10 do
    list.push("Item ${i}")
    i += 1
  end

  counter = 0
  for item, index in list do
    counter += 1
    break when index == 4
  end

  guava::assert(counter == 5)

  counter = 0
  for item, index in list do
    counter += 1
    break when index == 5
  end

  guava::assert(counter == 6)

  # repeat-loop
  sum = 0
  repeat 10 as i do
    sum += i
  end

  guava::assert(sum == 55)

  x = 2
  repeat 4 do
    x *= x
  end

  guava::assert(x == 65536)
end)

guava::register_test("try-finally", with () do
  a = 0, b = 0

  try
    a += 1
    println "${1 / 0}"
    a += 1
  finally
    b += 1
  end
  
  guava::assert(a == 1)
  guava::assert(b == 1)
end)

guava::register_test("try-catch-finally", with () do
  a = 0, b = 0, c = 0

  try
    for i in [0 .. 1] do
      a += 1
      println "${i / 0}"
      a += 1
    end
  catch (err)
    b += 1
  finally
    c += 1
  end

  guava::assert(a == 1)
  guava::assert(b == 1)
  guava::assert(c == 1)
end)

guava::register_test("nested try-catch", with () do
  a = 0, b = 0, c = 0

  for i in [0 .. 1] do
    try
      a += 1
      println "${i / 0}"
    catch (err, msg)
      b += 1
    finally
      c += 1
    end
  end

  guava::assert(a == 2)
  guava::assert(b == 2)
  guava::assert(c == 2)
end)

guava::register_test("lambdas", with () do
  # setup
  list = [{"id": 0}, {"id": 1}, {"id": 2}, {"id": 3}, {"id": 4},
          {"id": 5}, {"id": 6}, {"id": 7}, {"id": 8}, {"id": 9}]

  # Assigned and inline lambdas
  odd_item_id = with (item) do return item.id % 2 != 0 end # Define an assigned lambda.
  puts = with (s) do return s end # Defining a lambda and using it to wrap functionality.
  guava::assert(puts("Hello, World!") == "Hello, World!")

  # Lambdas as first-class citizens
  fn use_lambda(print_func, message) return print_func(message) end
  guava::assert(use_lambda(puts, "Hello, Kiwi!") == "Hello, Kiwi!")

  guava::assert(list.select(with (item) do return item["id"] % 2 == 0 end) == [{"id": 0}, {"id": 2}, {"id": 4}, {"id": 6}, {"id": 8}])
  guava::assert(list.select(odd_item_id) == [{"id": 1}, {"id": 3}, {"id": 5}, {"id": 7}, {"id": 9}])

  # List.map
  guava::assert
  (
    ["kiwi", "mango", "banana"].map(with (item) do return { "fruit": item, "is_a_kiwi": item.lowercase() == "kiwi" } end)
      == [{"fruit": "kiwi", "is_a_kiwi": true}, {"fruit": "mango", "is_a_kiwi": false}, {"fruit": "banana", "is_a_kiwi": false}]
  )

  # List.reduce
  numbers = [1, 2, 3, 4, 5]

  guava::assert((numbers.reduce(0, with (accumulator, number) do
    accumulator += number
  end)) == 15, "five")

  guava::assert((numbers.reduce({}, with (accumulator, number) do
    accumulator["key${number}"] = number
  end)) == {"key1": 1, "key2": 2, "key3": 3, "key4": 4, "key5": 5})

  # Inline lambda assignment
  x = (with (n) do n ** 2 end)(5)
  guava::assert(x == 25)
end)

testsuite()